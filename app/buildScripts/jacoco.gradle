apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.4"
    // Custom reports directory can be specfied like this:
    // reportsDir = file("$buildDir/customJacocoReportDir")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

project.afterEvaluate {

    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            task "create${sourceName.capitalize()}UnitTestCoverageReport"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

                reports {
                    xml.enabled true
                    html.enabled true
                }
                classDirectories =
                        fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/${sourcePath}",
                                excludes: ['**/R.class',
                                           '**/BR.class',
                                           // Remove Activity and application as they are dependent to Context
                                           '**/*Activity.class',
                                           '**/com/acemirr/training_task_1/ui/menu/grid/viewmodel/CustomGridDetailViewModelFactory.class',
                                           '**/com/acemirr/training_task_1/ui/menu/list/viewmodel/CustomListDetailViewModelFactory.class',
                                           '**/com/acemirr/training_task_1/ui/menu/paging/datasource/PagingDataSourceFactory.class',
                                           // Remove View Utils
                                           '**/com/acemirr/training_task_1/utils/AppExtensions.class',
                                           '**/com/acemirr/training_task_1/utils/BindingExtensions.class',

                                           '**/R$*.class',
                                           '**/*$ViewInjector*.*',
                                           '**/*$ViewBinder*.*',
                                           '**/BuildConfig.*',
                                           'android/**',
                                           '**/Manifest*.*',
                                           '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
                                           '**/*$*$*.*' // Anonymous classes generated by kotlin
                                ]
                        )
                sourceDirectories = files(["src/main/kotlin",
                                                 "src/$productFlavorName/kotlin",
                                                 "src/$buildTypeName/kotlin"])
                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
            }
        }
    }
}
